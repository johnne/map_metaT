# Main entrypoint of the workflow. 
# Please follow the best practices: 
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,
# in particular regarding the standardized folder structure mentioned there. 

configfile: "config/config.yml"
include: "rules/common.smk"

rule all:
    input:
        expand("results/{assembly}/{sample}.{suff}",
            assembly = assemblies.keys(), sample = samples.keys(), suff = ["bam", "bam.bai"])

rule align:
    input:
        fq1 = lambda wildcards: samples[wildcards.sample]["fq1"],
        fq2 = lambda wildcards: samples[wildcards.sample]["fq2"],
        asm = expand("{data}/{{assembly}}/final_contigs.fa.{n}.bt2l",
            n = ["1","2","3","rev.1","rev.2"], data=config["data_dir"])
    output:
        "results/{assembly}/{sample}.bam",
        "results/{assembly}/{sample}.bam.bai",
    log:
        "logs/{assembly}/{sample}.bt2.log"
    params:
        index = lambda wildcards, input: f"{os.path.dirname(input.asm[0])}/final_contigs.fa",
        temp_bam = "$TMPDIR/{assembly}.{sample}.bam",
        outdir = lambda wildcards, output: os.path.dirname(output[0])
    conda: "envs/bowtie2.yml"
    resources:
        runtime = lambda wildcards, attempt: attempt ** 2 * 60 * 10
    threads: 10
    shell:
        """
        bowtie2 --very-sensitive -1 {input.fq1} -2 {input.fq2} -p {threads} -x {params.index} 2> {log} | \
            samtools view -bh - | samtools sort - -o {params.temp_bam}
        samtools index {params.temp_bam}
        mv {params.temp_bam}* {params.outdir}
        """
